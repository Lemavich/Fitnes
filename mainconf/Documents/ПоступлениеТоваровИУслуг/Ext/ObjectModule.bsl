
Процедура ОбработкаПроведения(Отказ, Режим)
	СтруктураУчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если СтруктураУчетнаяПолитика.СпособУчетаЗапасов = Перечисления.СпособыСписанияЗапасов.FEFO Тогда
		ОтражатьСрокиГодности = Истина;
	Иначе
		ОтражатьСрокиГодности = Ложь;
	КонецЕсли; 
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
    
	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		Движение = Движения.УчетЗатрат.Добавить();
		Движение.Период = Дата;
		Движение.СтатьяЗатрат = ТекСтрокаУслуги.СтатьяЗатрат;
		Движение.Сумма = ТекСтрокаУслуги.Сумма;
	КонецЦикла;


	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);	
		
	Запрос.Текст="ВЫБРАТЬ
	             |	ПоступлениеТоваровИУслугТовары.Номенклатура КАК Номенклатура,
	             |	СУММА(ПоступлениеТоваровИУслугТовары.Количество) КАК Количество,
	             |	ПоступлениеТоваровИУслугТовары.Цена КАК Цена,
	             |	СУММА(ПоступлениеТоваровИУслугТовары.Сумма) КАК Сумма,
	             |	ПоступлениеТоваровИУслугТовары.СрокГодности КАК СрокГодности,
	             |	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	             |ИЗ
	             |	Документ.ПоступлениеТоваровИУслуг.Товары КАК ПоступлениеТоваровИУслугТовары
	             |ГДЕ
	             |	ПоступлениеТоваровИУслугТовары.Ссылка = &Ссылка
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ПоступлениеТоваровИУслугТовары.Номенклатура,
	             |	ПоступлениеТоваровИУслугТовары.Цена,
	             |	ПоступлениеТоваровИУслугТовары.СрокГодности,
	             |	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
	             |	ПоступлениеТоваровИУслугУслуги.Сумма КАК Сумма,
	             |	ПоступлениеТоваровИУслугУслуги.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	             |ИЗ
	             |	Документ.ПоступлениеТоваровИУслуг.Услуги КАК ПоступлениеТоваровИУслугУслуги
	             |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровИУслуг КАК ПоступлениеТоваровИУслуг
	             |		ПО ПоступлениеТоваровИУслугУслуги.Ссылка = ПоступлениеТоваровИУслуг.Ссылка
	             |ГДЕ
	             |	ПоступлениеТоваровИУслуг.Ссылка = &Ссылка
	             |ИТОГИ ПО
	             |	СтатьяЗатрат";
	
	РезультатПакет = Запрос.ВыполнитьПакет();
	ВыборкаТовары = РезультатПакет[0].Выбрать();
	СчетКредита = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;

	Пока ВыборкаТовары.Следующий() Цикл
		Движение = Движения.ТоварыНаСкладах.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаТовары.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = ВыборкаТовары.Количество;
        Если ОтражатьСрокиГодности Тогда
			Движение.СрокГодности= ВыборкаТовары.СрокГодности;
		КонецЕсли;                                      
		Движение.Сумма = ВыборкаТовары.Сумма; 
		//Регистр бухгалтерии
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = ВыборкаТовары.СчетБухгалтерскогоУчета;
		Движение.СчетКт = СчетКредита;
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаТовары.Сумма;
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт,Движение.СубконтоДт,ВыборкаТовары.Номенклатура);
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт,Движение.СубконтоКт,Контрагент);
	КонецЦикла;
	
	ВыборкаУслгуи = РезультатПакет[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаУслгуи.Следующий() Цикл
		Выборка=ВыборкаУслгуи.Выбрать();
		Пока Выборка.Следующий() Цикл
			Движение = Движения.Хозрасчетный.Добавить();   
			Движение.СчетДт = Выборка.СчетБухгалтерскогоУчета;
			Движение.СчетКт = СчетКредита;
			Движение.Период = Дата;
			Движение.Сумма = Выборка.Сумма;
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт,Движение.СубконтоДт,Выборка.СтатьяЗатрат);
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт,Движение.СубконтоКт,Контрагент);
		КонецЦикла;
	КонецЦикла;
	

	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	СуммаДокумента = Товары.Итог("Сумма")+Услуги.Итог("Сумма");
КонецПроцедуры
