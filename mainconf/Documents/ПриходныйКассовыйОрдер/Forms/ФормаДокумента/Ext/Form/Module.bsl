&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Основание")
	    И ЗначениеЗаполнено(Параметры.Основание)
	    И ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.РеализацияТоваровИУслуг") Тогда 
	    СтруктураОплаты = Документы.РеализацияТоваровИУслуг.ПроверитьОплатуДокумента(Параметры.Основание);
	    ПризнакОплаты = СтруктураОплаты.ПризнакОплаты;

	    Если ПризнакОплаты = Перечисления.ОплатаДокумента.ПолностьюОплачен Тогда
	        СообщениеПользователю = Новый СообщениеПользователю(); 
	        СообщениеПользователю.Текст = "Данный документ уже полностью оплачен. Ввод дополнительного документа оплаты не требуется!";
	        СообщениеПользователю.Сообщить();
	        Отказ = Истина;
	        возврат;
	    Иначе
	        Объект.СуммаДокумента = СтруктураОплаты.ОсталосьОплатить;
			ОсталосьОплатить = СтруктураОплаты.ОсталосьОплатить;
	    КонецЕсли;
	КонецЕсли;

	ЗаполнитьРеквизитыФормы();

КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
    Если ОсталосьОплатить > 0 И Объект.СуммаДокумента > ОсталосьОплатить Тогда
        Объект.СуммаДокумента = ОсталосьОплатить;
    КонецЕсли;    
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	Плательщик = Объект.Плательщик;
	ВидОперации = Объект.ВидОперации;
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ВидОперации = Перечисления.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры


&НаКлиенте
Процедура УстановитьВидимость()
    
    СтрокаТипПлательщика = "";
	
	
    Если Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника") Тогда
        Элементы.Договор.Видимость = Ложь;          
        Элементы.Плательщик.Видимость = Истина;
        Элементы.Касса.Видимость = Истина;
        СтрокаТипПлательщика = "СправочникСсылка.Сотрудники";
    ИначеЕсли Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПоставщика") Тогда
        Элементы.Договор.Видимость = Истина;    
        Элементы.Плательщик.Видимость = Истина;
        Элементы.Касса.Видимость = Истина;
        СтрокаТипПлательщика = "СправочникСсылка.Контрагенты";
    Иначе
        Элементы.Договор.Видимость = Ложь;    
        Элементы.Плательщик.Видимость = Истина;
        Элементы.Касса.Видимость = Истина;
        СтрокаТипПлательщика = "СправочникСсылка.Контрагенты";
    КонецЕсли;
    
    Если ЗначениеЗаполнено(СтрокаТипПлательщика) Тогда         
        Массив = Новый Массив();
        Массив.Добавить(Тип(СтрокаТипПлательщика));    
        ОписаниеТипаПлательщика = Новый ОписаниеТипов(Массив);
        Элементы.Плательщик.ОграничениеТипа = ОписаниеТипаПлательщика;        
    КонецЕсли;  
    
    Если Элементы.Договор.Видимость = Истина И ЗначениеЗаполнено(Объект.Плательщик) Тогда
        Элементы.Договор.Видимость = ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Объект.Плательщик);
    КонецЕсли;
    
КонецПроцедуры       

&НаСервереБезКонтекста
Функция ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Плательщик)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Плательщик", Плательщик);
	Запрос.Текст = "ВЫБРАТЬ
	               |    Контрагенты.ТипКонтрагента КАК ТипКонтрагента,
	               |    Контрагенты.Ссылка КАК Контрагент
	               |ИЗ
	               |    Справочник.Контрагенты КАК Контрагенты
	               |ГДЕ
	               |    Контрагенты.Ссылка = &Плательщик";

	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	Если ЗначениеЗаполнено(Выборка.ТипКонтрагента)
	    И Выборка.ТипКонтрагента = Перечисления.ТипыКонтрагентов.Клиент Тогда
	    возврат Ложь;
	Иначе
	    возврат Истина;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	МассивОчищаемыхРеквизитов = Новый Массив;
	Если Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника")
	    И (ЗначениеЗаполнено(Объект.Плательщик) ИЛИ ЗначениеЗаполнено(Объект.Договор)) Тогда
	    МассивОчищаемыхРеквизитов.Добавить("Договор");
	    МассивОчищаемыхРеквизитов.Добавить("Плательщик");

	    ТекстВопроса = "При изменении реквизита ""ВидОперации"" будут очищены следующие данные:
	    | - Договор
	    | - Плательщик
	    | Продолжить?";

	КонецЕсли;

	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
	    Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииВидаОперации", ЭтаФорма, МассивОчищаемыхРеквизитов);
	    ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Внимание!");
	Иначе
	    ВидОперации =  Объект.ВидОперации;
	    УстановитьВидимость();
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииВидаОперации(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
	    Для Каждого Реквизит Из ДополнительныеПараметры Цикл
	        Объект[Реквизит] = Неопределено;
	    КонецЦикла;
	    УстановитьВидимость();
	    ВидОперации =  Объект.ВидОперации;
	Иначе
	    Объект.ВидОперации = ВидОперации;
	КонецЕсли;
КонецПроцедуры  

&НаКлиенте
Процедура ПлательщикПриИзменении(Элемент)
	ЭтоПоставщик = ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Объект.Плательщик);
	МассивОчищаемыхРеквизитов = Новый Массив;

	Если НЕ ЭтоПоставщик И ЗначениеЗаполнено(Объект.Договор) Тогда
	    МассивОчищаемыхРеквизитов.Добавить("Договор");

	    ТекстВопроса = "При изменении реквизита ""Плательщик"" будут очищены следующие данные:
	    | - Договор
	    | Продолжить?";
	КонецЕсли;

	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
	    Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииПлательщик", ЭтаФорма, МассивОчищаемыхРеквизитов);
	    ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Внимание!");
	Иначе
	    Плательщик =  Объект.Плательщик;
	    УстановитьВидимость();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииПлательщик(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
	    Для Каждого Реквизит Из ДополнительныеПараметры Цикл
	        Объект[Реквизит] = Неопределено;
	    КонецЦикла;
	    УстановитьВидимость();
	    Плательщик =  Объект.Плательщик;
	Иначе
	    Объект.Плательщик = Плательщик;
	КонецЕсли;
КонецПроцедуры


